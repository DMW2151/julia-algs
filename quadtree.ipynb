{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Learning Julia 1.0 - Implement Quad-Trees in Julia\n",
    "Work towards an efficient Quadtree for analysis on geospatial datasets Adapted from [GeeksForGeeks](https://www.geeksforgeeks.org/quad-tree/) datatype deesctiption and [Wikipedia](https://en.wikipedia.org/wiki/Quadtree#Pseudocode)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Box"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "const QT_MAX_PTS = 4\n",
    "\n",
    "\"\"\" AbstractType for 2D shapes w. 4 corners \"\"\"\n",
    "abstract type AbstractBox end\n",
    "\n",
    "\"\"\" Basic point structure with lng(x), and lat(y) \"\"\"\n",
    "struct Coord \n",
    "    lng :: Float32\n",
    "    lat :: Float32\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Used for search on qtBox, defined by the \n",
    "SW-corner and (SW.lng + side, SW.lat + side)\n",
    "\"\"\"\n",
    "struct Box <: AbstractBox\n",
    "    SW :: Coord\n",
    "    sideLength :: Float32\n",
    "end \n",
    "\n",
    "\"\"\"\n",
    "Generate uniform random points along a surface\n",
    "e.g. generateRandUniform(0., 10., 0., 10., 1) -> Array{Float64,2}:  2.19137  1.57839\n",
    "\"\"\"\n",
    "function generateRandUniform(x0::Float64, x1::Float64, y0::Float64, y1::Float64, n::Int)::Array{Coord}\n",
    "    X = ((ones(n) * x0) + (rand(n) * (x1 - x0))) \n",
    "    Y = ((ones(n) * y0) + (rand(n) * (y1 - y0)))\n",
    "    return [Coord(x, y) for (x,y) in zip(X,Y)]\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Same as Box. Adds references to set of points and set of children qtBoxes\n",
    "\n",
    "see: \n",
    "    - https://github.com/JuliaLang/julia/blob/master/base/pointer.jl\n",
    "    - https://stackoverflow.com/questions/52329949/how-to-define-the-type-of-a-binary-search-tree-in-julia\n",
    "\"\"\"\n",
    "mutable struct qtBox <: AbstractBox\n",
    "    SW :: Coord\n",
    "    sideLength :: Float32 # sideLength > 0 \n",
    "    children :: Union{Array{qtBox}, Nothing} #Initialize on subdivide\n",
    "    points ::  Union{Array{Coord}, Nothing}\n",
    "end\n",
    "\n",
    "\"\"\" Default constructor initializes a qtBox with no children or points\"\"\"\n",
    "qtBox(c::Coord, s::Float32) = qtBox(c, s, nothing, nothing); \n",
    "\n",
    "\n",
    "\"\"\"Check if point (p) is contained in a Box\"\"\"\n",
    "function checkPoint(p::Coord, region::AbstractBox)::Bool\n",
    "    # Check if point is to the east and north of the box's SW point \n",
    "    return (\n",
    "        (p.lng > region.SW.lng) & (p.lng < region.SW.lng + region.sideLength) &&\n",
    "        (p.lat > region.SW.lat) & (p.lat < region.SW.lat + region.sideLength)\n",
    "    )\n",
    "    end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Split a qtBox into 4 equal parts\n",
    "Assuming r has Coord(0. 0.) and sideLength = 2\n",
    "e.g. subdivide!(r) create new qtBoxes as follows:\n",
    "    - ((0,0), 1)\n",
    "    - ((1,0), 1)\n",
    "    - ((0,1), 1)\n",
    "    - ((1,1), 1)\n",
    "\n",
    "Split a node into the 4 subnodes evenly into squares\n",
    " _ _ _ _ \n",
    "|NW |NE | \n",
    "|_ _|_ _|\n",
    "|SW |SE |\n",
    "|_ _|_ _|\n",
    "\n",
    "\"\"\"\n",
    "function subdivide!(r::qtBox)\n",
    "    \n",
    "    if r.children === nothing # If not yet subdivided\n",
    "        nodeDelta = r.sideLength / 2 # New sideLength\n",
    "        \n",
    "        # Add each of the children to Array...\n",
    "        r.children = Array([\n",
    "            qtBox(r.SW, nodeDelta), # S.W Child (+0, +0)\n",
    "            qtBox(Coord(r.SW.lng + nodeDelta, r.SW.lat), nodeDelta), # S.E Child (+1 , +0),\n",
    "            qtBox(Coord(r.SW.lng, r.SW.lat + nodeDelta), nodeDelta), #N.W child (+0, +1)\n",
    "            qtBox(Coord(r.SW.lng + nodeDelta, r.SW.lat + nodeDelta), nodeDelta) # N.E child (+1, +1)\n",
    "            ])\n",
    "        end \n",
    "    end \n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Check if a Box (s1) wholly contains a qtBox (s0) \n",
    "\n",
    "Because qtBox is square, The only checks required are:\n",
    "    - s1 SW point LESS (more west) than s0's\n",
    "    - s1 SE point MORE (more east) than s0's\n",
    "\"\"\"\n",
    "function regionContains(s0::qtBox, s1::AbstractBox)::Bool\n",
    "    return (s0.SW.lng >= s1.SW.lng) && \n",
    "        ((s0.SW.lng + s0.sideLength) <= (s1.SW.lng + s1.sideLength))\n",
    "    end \n",
    "\n",
    "\n",
    "\"\"\" Check if a Box (s1) intersects/overlaps a qtBox (s0)\"\"\"\n",
    "function regionOverlap(s0::qtBox, s1::Box)::Bool\n",
    "    # If one rectangle is on left side of other \n",
    "    # OR If one rectangle is above other\n",
    "    if (\n",
    "        (s0.SW.lng > (s1.SW.lng + s1.sideLength)) ||\n",
    "        (s1.SW.lng > (s0.SW.lng + s0.sideLength)) || \n",
    "        (s0.SW.lat > (s1.SW.lat + s1.sideLength)) || \n",
    "        (s1.SW.lat > (s0.SW.lat + s0.sideLength))\n",
    "        )\n",
    "        return false\n",
    "    else \n",
    "        return true\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\" \n",
    "Recursively insert points into a qtBox\n",
    "See Wikipedia description:\n",
    "    - https://en.wikipedia.org/wiki/Quadtree#Pseudocode\n",
    "\"\"\"\n",
    "function insertIntoQuadTree!(r::qtBox, ps::Coord)::Bool        \n",
    "        \n",
    "    # Ignore if point is outside range of parent\n",
    "    if !checkPoint(ps, r)\n",
    "        return false\n",
    "    end\n",
    "\n",
    "    # If parent has room in point set and has not been subdivided, initialize \n",
    "    if r.points === nothing\n",
    "        r.points = Array{Coord}([ps])\n",
    "        return true\n",
    "    \n",
    "    # See const QT_MAX_PTS; If there is room in a parent point set, add to parent\n",
    "    elseif (length(r.points) < QT_MAX_PTS)\n",
    "        push!(r.points, ps)\n",
    "        return true\n",
    "    \n",
    "    # If cannot push to parent and there are children, create children\n",
    "    elseif r.children === nothing \n",
    "        subdivide!(r) \n",
    "    end\n",
    "    \n",
    "    # Now that there are children, insert\n",
    "    if (r.children !== nothing)\n",
    "        for c in r.children \n",
    "            if insertIntoQuadTree!(c, ps)\n",
    "                return true\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return false # Fatal - Should not happen\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Recursively query a range on a qtBox, includes a case for handling when\n",
    "there is a complete overlap of the query space with the search space\n",
    "see:\n",
    "    - https://en.wikipedia.org/wiki/Quadtree#Pseudocode\n",
    "\"\"\"\n",
    "function queryRange(r::qtBox, qryBox::AbstractBox)::Array{Coord}\n",
    "\n",
    "    # Initialize return list\n",
    "    pointsInRange = Array{Coord}(Array[])\n",
    "    \n",
    "    # Exit if the range does not intersect this quad\n",
    "    if !regionOverlap(r, qryBox)\n",
    "        return pointsInRange\n",
    "\n",
    "        \n",
    "    elseif regionContains(r, qryBox) \n",
    "        # If  full region overlap then add all points w./o checks\n",
    "        if r.points !== nothing\n",
    "            # Append parent points - No Check\n",
    "            append!(pointsInRange, r.points) \n",
    "        end\n",
    "        \n",
    "        # Append children's Point's - No Check\n",
    "        if r.children !== nothing \n",
    "            for c in r.children\n",
    "                append!(pointsInRange, queryRange(c, qryBox))\n",
    "            end\n",
    "        end\n",
    "    \n",
    "    else \n",
    "        # If partial overlap check each point on range 1-by-1    \n",
    "        if r.points !== nothing\n",
    "            for p in r.points\n",
    "                if checkPoint(p, qryBox)\n",
    "                    push!(pointsInRange, p)\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    \n",
    "        # Append children's point's - With 1-by-1 check\n",
    "        if r.children === nothing\n",
    "            return pointsInRange # No children, exit\n",
    "        else \n",
    "            # recursively Add the points from the children\n",
    "            for c in r.children \n",
    "                append!(pointsInRange, queryRange(c, qryBox))\n",
    "            end\n",
    "        end\n",
    "        \n",
    "    end\n",
    "    \n",
    "    return pointsInRange \n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "getLocation = x::Coord -> (x.lat, x.lng)\n",
    "\n",
    "\"\"\"Wrapper to getLocation function, extracts lat and lng attributes from AbstractBox\"\"\"\n",
    "function partialCoordComp(ps::Array{Coord}, t::Coord)::Bool\n",
    "    return getLocation(t) in map(getLocation, ps)\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Removes point from tree structure, find the point (t) and remove\n",
    "WIP: Option to run intermediate cleaup on the parents of the removed point\n",
    "see:\n",
    "    - https://stackoverflow.com/a/9387997\n",
    "\"\"\"\n",
    "function removePointRebuild!(r::qtBox, t::Coord, modifyStructure::Bool)::Bool\n",
    "    # Skip removing point if outside of parent range\n",
    "    if !checkPoint(t, r) \n",
    "        return false\n",
    "    end\n",
    "    \n",
    "    # Check equality, check to see if coordinates are EXACTLY in parent's set of points\n",
    "    if r.points !== nothing\n",
    "        if partialCoordComp(r.points, t) \n",
    "            filter!(x -> x != t, r.points)\n",
    "            return true\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # WIP: CHECK THIS BLOCK\n",
    "    if modifyStructure # TODO: Cobbled this together w. no external reference, plz check again...\n",
    "        if r.children !== nothing\n",
    "            for c in r.children # Search the space, each of children\n",
    "                if c.points !== nothing\n",
    "                    if partialCoordComp(c.points, t) \n",
    "                        filter!(x -> x != t, c.points) # if in child, remove and return true\n",
    "\n",
    "                        # Modify Structure Sequence: If no children have points, \n",
    "                        # delete this qtBox's children by setting children back \n",
    "                        # to `Array{qtBox}([])`, TODO: Check Array{qtBox}([qtBox(Coord(1., 1.), 1.)])?\n",
    "                        if sum(x -> x = (x.points !== nothing), r.children) == 0\n",
    "                            r.children = Array{qtBox}([])\n",
    "                        end\n",
    "\n",
    "                        return true\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "        \n",
    "    # If parent has children, recursively check to see if point is there\n",
    "    if r.children !== nothing\n",
    "        for c in r.children\n",
    "            if removePointRebuild!(c, t, modifyStructure)\n",
    "                return true\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return false #Could not remove point...\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Calculate Haversine Distance between two Coords. Formula \n",
    "calculates great-circle distance between two points \n",
    "Modified from:\n",
    "    - https://www.movable-type.co.uk/scripts/latlong.html\n",
    "\"\"\"\n",
    "function haversineDistance(p1::Coord, p2::Coord)::Float32\n",
    "    lat1, lat2 = p1.lat, p2.lat    \n",
    "    d_lat = deg2rad(lat2 - lat1)\n",
    "\n",
    "    A = (sin(d_lat/2) * sin(d_lat/2)) + cos(deg2rad(lat1)) * cos(deg2rad(lat2)) * sin((p2.lng - p2.lng)/2)^2\n",
    "\n",
    "    return Float32(6372.8) * Float32(2 * atan(sqrt(A), sqrt(1-A)))\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\" Get the bounding box implied from a center coordinate and a radius \"\"\"\n",
    "function getSearchRange(c::Coord, radius::Union{Float32, Float64})::Box\n",
    "    return Box(Coord(c.lng - radius/√2 , c.lat - radius/√2), (2*radius)/√2)\n",
    "    end\n",
    "\n",
    "\n",
    "\"\"\" Get all values in a given radius of a Coord \"\"\"\n",
    "function radialSearch(r::qtBox, c::Coord, d::Union{Float32, Float64})::Array{Coord}\n",
    "    # Get first square\n",
    "    return filter(x -> x = haversineDistance(x, c) < d, queryRange(r, getSearchRange(c, d)))\n",
    "end "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Appendix + Notes\n",
    "---------------------------------------------------------------------------"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(4, 500000, 100000)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using BenchmarkTools\n",
    "\n",
    "const QT_MAX_PTS, QT_TEST_SIZE, QT_TEST_RM_SIZE = 4, 500_000, 100_000"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Benchmark Creation and Query Statistics - 500,000 points \n",
    "\n",
    "- Insert Test: Insert 500,000 random points on ((0, 1), (0, 1))\n",
    "- Object Size: \n",
    "- Box Search: Query bounding box w. SW corner on ((0, 1), (0, 1)) with sideLength (0, 0.5)\n",
    "- Radial Search: Query circle w. center on ((0, 1), (0, 1)) and radius of 5,000m (~0.045 in cartesian plane)\n",
    "\n",
    "\n",
    "### Running Notes on Performance/Space Increases"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  1.136342 seconds (12.58 M allocations: 410.055 MiB, 16.07% gc time)\n"
     ]
    }
   ],
   "source": [
    "## Inserts - Tree Build\n",
    "ps = generateRandUniform(0., 1., 0., 1., QT_TEST_SIZE);\n",
    "\n",
    "#TODO: constructor should take anyFloat\n",
    "sampleQTree = qtBox(Coord(Float32(0.), Float32(0.)), Float32(1.))\n",
    "\n",
    "@time (for p in ps insertIntoQuadTree!(sampleQTree, p) end)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\"Total Obj Size: 26.158232 MB\""
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"Total Obj Size: $(Base.summarysize(sampleQTree) / 1000000 ) MB\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "BenchmarkTools.Trial: \n",
       "  memory estimate:  8.03 KiB\n",
       "  allocs estimate:  146\n",
       "  --------------\n",
       "  minimum time:     8.244 μs (0.00% GC)\n",
       "  median time:      3.121 ms (0.00% GC)\n",
       "  mean time:        5.936 ms (8.01% GC)\n",
       "  maximum time:     27.839 ms (10.10% GC)\n",
       "  --------------\n",
       "  samples:          843\n",
       "  evals/sample:     1"
      ]
     },
     "execution_count": 13,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@benchmark queryRange(sampleQTree, Box( Coord(rand() , rand()), rand()/2))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "BenchmarkTools.Trial: \n",
       "  memory estimate:  138.21 MiB\n",
       "  allocs estimate:  1180816\n",
       "  --------------\n",
       "  minimum time:     126.227 ms (4.61% GC)\n",
       "  median time:      131.744 ms (6.61% GC)\n",
       "  mean time:        135.798 ms (9.15% GC)\n",
       "  maximum time:     233.347 ms (44.77% GC)\n",
       "  --------------\n",
       "  samples:          37\n",
       "  evals/sample:     1"
      ]
     },
     "execution_count": 16,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "#Test in kilometers, 5km ~0.045 on a flat plane\n",
    "@benchmark radialSearch(sampleQTree, Coord(rand(), rand()), 5.) "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "BenchmarkTools.Trial: \n",
       "  memory estimate:  887.21 MiB\n",
       "  allocs estimate:  28974667\n",
       "  --------------\n",
       "  minimum time:     2.476 s (3.37% GC)\n",
       "  median time:      2.494 s (3.41% GC)\n",
       "  mean time:        2.491 s (3.62% GC)\n",
       "  maximum time:     2.504 s (4.07% GC)\n",
       "  --------------\n",
       "  samples:          3\n",
       "  evals/sample:     1"
      ]
     },
     "execution_count": 17,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Oyy, Benchmarking function!() is pretty expensive, but passing Modify == false is ~3-4x quicker\n",
    "removeCoords = ps[map(x -> Int64(floor(x + 1)), rand(QT_TEST_RM_SIZE) * QT_TEST_RM_SIZE)]\n",
    "@benchmark (for rmc in removeCoords removePointRebuild!(sampleQTree, rmc, true) end)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.4.0",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
